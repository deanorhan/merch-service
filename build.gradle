plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
	id 'jacoco'
	id 'org.owasp.dependencycheck' version '7.1.1'
	id "com.github.andygoossens.gradle-modernizer-plugin" version "1.6.2"
	// Fucking buildship
	id 'eclipse'
}

group = 'org.daemio'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
		}
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly

	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.mapstruct:mapstruct:1.5.2.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

	annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	runtimeOnly 'com.h2database:h2'

	implementation 'org.springdoc:springdoc-openapi-ui:1.6.10'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	integrationTestImplementation('io.rest-assured:rest-assured:5.1.1') {
		exclude group: 'org.apache.groovy', module: 'groovy'
		exclude group: 'org.apache.groovy', module: 'groovy-json'
		exclude group: 'org.apache.groovy', module: 'groovy-xml'
	}
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
}

tasks.withType(Test) {
	useJUnitPlatform()
}

jacocoTestReport {
	getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
	
	reports {
		xml.required = true
	}
}

check.dependsOn integrationTest

// This and the eclipse plugin inclusion are only here because buildship is shit
eclipse {
	classpath {
		containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
		file.whenMerged { cp ->
			def entries = cp.entries;
			def src = new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main/', null)
			entries.add(src)
		} 
	}
}